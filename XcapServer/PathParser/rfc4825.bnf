;@using "rfc4234.core.rules.bnf"
;@using "rfc3986.bnf"

AUID             =  global-a-uid / vendor-a-uid
global-a-uid     =  a-uid
a-uid            =  1*a-uid-char
vendor-a-uid     =  rev-hostname "." a-uid
rev-hostname     =  toplabel *( "." domainlabel  )
domainlabel      =  alphanum
                    / alphanum *( alphanum / "-" ) alphanum
toplabel         =  ALPHA / ALPHA *( alphanum / "-" ) alphanum
a-uid-char       =  a-uid-unreserved / pct-encoded / sub-delims
                    / ":" / "@"
                               ;pct-encoded from RFC 3986
                               ;sub-delims from RFC 3986
alphanum         = ALPHA / DIGIT
                               ;DIGIT from RFC 4234
                               ;ALPHA from RFC 4234
a-uid-unreserved = ALPHA / DIGIT / "-" / "_" / "~"


node-selector          = element-selector ["/" terminal-selector]
terminal-selector      = attribute-selector / namespace-selector /
                         extension-selector
element-selector       = step *( "/" step)
step                   = by-name / by-pos / by-attr / by-pos-attr /
                         extension-selector
by-name                = NameorAny
by-pos                 = NameorAny "[" position "]"

position               = 1*DIGIT
attr-test              = "@" att-name "=" att-value
by-attr                = NameorAny "[" attr-test "]"
by-pos-attr            = NameorAny "[" position "]" "[" attr-test "]"
NameorAny              = QName / "*"   ; QName from XML Namespaces
att-name               = QName
att-value              = AttValue      ; from XML specification
attribute-selector     = "@" att-name
namespace-selector     = "namespace::*"
extension-selector     = 1*( %x00-2e / %x30-ff )  ; anything but "/"
